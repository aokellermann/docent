resource "aws_db_subnet_group" "main" {
  name       = "${var.project_name}-${var.deployment}-db-subnet-group"
  subnet_ids = aws_subnet.private[*].id

  tags = {
    Name        = "${var.project_name}-${var.deployment}-db-subnet-group"
    Deployment = var.deployment
  }
}

resource "aws_db_instance" "postgres" {
  identifier = "${var.project_name}-${var.deployment}-postgres"

  engine         = "postgres"
  engine_version = "15.12"
  instance_class = var.rds_instance_class

  allocated_storage     = 20
  max_allocated_storage = var.rds_max_allocated_storage
  storage_type          = "gp3"
  storage_encrypted     = true

  db_name  = var.db_name
  username = var.db_username
  password = var.db_password

  db_subnet_group_name   = aws_db_subnet_group.main.name
  vpc_security_group_ids = [aws_security_group.rds.id]
  publicly_accessible    = false

  backup_retention_period = 7
  backup_window          = "03:00-04:00"
  maintenance_window     = "sun:04:00-sun:05:00"

  deletion_protection = true
  skip_final_snapshot = false
  final_snapshot_identifier = "${var.project_name}-${var.deployment}-postgres-final-snapshot"

  tags = {
    Name        = "${var.project_name}-${var.deployment}-postgres"
    Deployment = var.deployment
  }
}
